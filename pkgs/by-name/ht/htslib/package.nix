{
  lib,
  stdenv,
  fetchurl,
  zlib,
  bzip2,
  xz,
  curl,
  libdeflate,
  perl,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "htslib";
  version = "1.24";

  src = fetchurl {
    url = "https://github.com/samtools/htslib/releases/download/${finalAttrs.version}/htslib-${finalAttrs.version}.tar.bz2";
    hash = "sha256-KKjeGROBx6l6NWdc6sdvoeqV57Z41qLp1gCnh05Ad94=";
  };

  # perl is only used during the check phase.
  nativeBuildInputs = [ perl ];

  buildInputs = [
    zlib
    bzip2
    xz
    curl
    libdeflate
  ];

  configureFlags =
    if !stdenv.hostPlatform.isStatic then
      [ "--enable-libcurl" ] # optional but strongly recommended
    else
      [
        "--disable-libcurl"
        "--disable-plugins"
      ];

  # In the case of static builds, we need to replace the build and install phases
  buildPhase = lib.optionalString stdenv.hostPlatform.isStatic ''
    make AR=$AR lib-static
    make LDFLAGS=-static bgzip htsfile tabix
  '';

  installPhase = lib.optionalString stdenv.hostPlatform.isStatic ''
    install -d $out/bin
    install -d $out/lib
    install -d $out/include/htslib
    install -D libhts.a $out/lib
    install  -m644 htslib/*h $out/include/htslib
    install -D bgzip htsfile tabix $out/bin
  '';

  preCheck = ''
    patchShebangs test/
  '';

  enableParallelBuilding = true;

  doCheck = true;

  meta = {
    description = "C library for reading/writing high-throughput sequencing data";
    license = lib.licenses.mit;
    homepage = "http://www.htslib.org/";
    platforms = lib.platforms.unix;
    maintainers = [ lib.maintainers.mimame ];
  };
})
